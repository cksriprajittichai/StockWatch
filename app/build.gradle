apply plugin: 'com.android.application'

/* Create a variable called keystorePropertiesFile, and initialize it to my
 * keystore.properties file, in the rootProject folder. */
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties
def keystoreProperties = new Properties()

// Load my keystore.properties file into the keystoreProperties object
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))


android {
    compileSdkVersion 28
    useLibrary 'org.apache.http.legacy' // For ACRA
    defaultConfig {
        applicationId "c.chasesriprajittichai.stockwatch"
        minSdkVersion 24
//         minSdkVersion 26 // java.time.* requires API >= 26
        targetSdkVersion 28
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [HOCKEYAPP_APP_ID: "7a66f7ec251949d88f47aaa4931f20cc"]
        versionCode = 1
        versionName = "1.0.0"
    }
    signingConfigs { // This signingConfigs block must be above the buildTypes block
        config {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }
    buildTypes {
        debug {
            versionNameSuffix = ".debug"
            applicationIdSuffix = ".debug"
        }
        release {
            versionNameSuffix = ""
            applicationIdSuffix = ""
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    productFlavors {
    }
}

configurations {
    all {
        /* org.json:json:20180130 causes the following warning:
         * "json defines classes that conflict with classes now provided by
         * Android. Solutions include finding newer versions or alternative
         * libraries that don't have the same problem (for example, for
         * httpclient use HttpUrlConnection or okhttp instead), or repackaging
         * the library using something like jarjar."
         *
         * This prevents the warning (the warning is an error when generating
         * a release APK.
         */
        exclude module: 'json'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    //noinspection GradleCompatible
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'com.android.support:support-v4:28.0.0-rc02'
    implementation 'com.android.support:appcompat-v7:28.0.0-rc02'
    implementation 'com.android.support:recyclerview-v7:28.0.0-rc02'
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    implementation 'com.android.volley:volley:1.1.0'
    implementation 'org.jsoup:jsoup:1.11.3'
    implementation 'org.json:json:20180130'
    implementation files('libs/commons-lang3-3.7.jar')
    implementation 'com.robinhood.spark:spark:1.2.0'
    implementation 'com.wefika:horizontal-picker:1.1.1'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.1.0'
    implementation 'ch.acra:acra-core:5.1.3'
    implementation 'net.hockeyapp.android:HockeySDK:5.1.0'
}
